import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id "java"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2017.10.30"
}

apply plugin: 'org.junit.platform.gradle.plugin'

def TEAM = 1540
def ROBOT_CLASS = "org.team1540.kingbass.Robot"

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile wpilib()
    compile navx()
    compile ctre()
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.0.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.1'
}

// setting the source directory for all the code
sourceSets {
    main {
        java {
            srcDirs = ['src/']
        }
    }
    test {
        java {
            srcDirs = ['test/']
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}
task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}